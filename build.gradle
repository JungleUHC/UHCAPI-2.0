plugins {
    id 'java'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
    id 'com.palantir.git-version' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'fr.altaks.uhcapi2'
version = "${versionDetails().lastTag}-${versionDetails().branchName}-${versionDetails().gitHash}"

repositories {
    mavenCentral() // Main Maven repository
    mavenLocal() // This is needed for CraftBukkit and Spigot.
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' // Spigot's Maven repository
        content {
            includeGroup 'org.bukkit' // Include the Bukkit API
            includeGroup 'org.spigotmc' // Include the Spigot API
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.enginehub.org/repo/' }

}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT' // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT' // The full Spigot server with no shadowing.

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    // https://mvnrepository.com/artifact/com.github.seeseemelk/MockBukkit-v1.13
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.13:0.2.0'

}

def getGitAuthors = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'log','--pretty=\"%an\"';
        standardOutput = stdout
    }
    ArrayList<String> withoutDuplicates = [];
    stdout.toString().eachLine { it ->
        if(!withoutDuplicates.contains(it)){
            withoutDuplicates.add(it);
        }
    }
    StringJoiner finalResult = new StringJoiner(", ");
    withoutDuplicates.forEach {it -> finalResult.add(it)}
    return finalResult.toString().trim()
}

processResources {
    dependsOn(clean)
    filter {
        it.replace('${project.version}', "${version}").replace('\"${project.authors}\"', "${getGitAuthors()}")
    }
}

idea {
    module {
        inheritOutputDirs = true
        name = "spigot-template-${version}"
        downloadJavadoc = true
        downloadSources = true
    }
}



test {
    useJUnitPlatform() // JUnit 5
}

shadowJar {
    // Replace 'com.yourpackage' with the package of your plugin
    relocate 'fr.mrmicky.fastinv', 'fr.altaks.fastinv'
}
